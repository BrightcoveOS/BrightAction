<?xml version="1.0" encoding="utf-8"?>
<!--
    A basic example of loading a DataGrid with Video information, without using BrightAction.

-->
<mx:Application 
    xmlns:mx="http://www.adobe.com/2006/mxml">
    
    <!--
    ********************************************************************************************
    SCRIPT
    ********************************************************************************************
    -->
    <mx:Script>
        <![CDATA[
            import com.adobe.serialization.json.JSON;
            import mx.collections.ArrayCollection;
            import mx.controls.Alert;
            
            /** The object used to load URLs */
            private var _loader:URLLoader;
            
            /** The path to the brightcove api services */
            private const BRIGHTCOVE_API_PATH:String = "http://api.brightcove.com/services/library";
            
            /** The publisher token to be used to make the requests */
            private var _token:String = "Q1kGwkwut5YvD4cGLykpwxNQREpbBRYHCEZu2LF8z6U.";
            
            /** The command string to find all videos*/
            private const FIND_ALL_COMMAND_STRING:String = "find_all_videos";
            
            /** 
             * The "click" event listener for the find_all_videos button - load the videos from 
             * Brightcove convert them for JSON to native AS Objects and populate it in a datagrid
             */  
            private function onButtonClick():void {
                
                var url:String = BRIGHTCOVE_API_PATH + 
                    "?command=" + 
                    FIND_ALL_COMMAND_STRING +
                    "&token=" +
                    _token;
                
                trace('url to request: ' + url);
                var request:URLRequest = new URLRequest(url);
                
                _loader = new URLLoader();
                _loader.addEventListener(IOErrorEvent.IO_ERROR, errorHandler);
                _loader.addEventListener(Event.COMPLETE, loaderCompleteHandler);
                
                try {
                    _loader.load(request);
                }
                catch (error:SecurityError) {
                    trace("A SecurityError has occurred.");
                }
                
            }
            
            /** "fault" event listener to the remote call to the Brightcove media APIs */ 
            private function errorHandler(event:Event):void {
                
                mx.controls.Alert.show(
                    "An error occurred" + 
                    event.target.toString(),
                    "An error occurred");
                
            }
            
            /** "result" event listener to the remote call to the Brightcove media APIs */ 
            private function loaderCompleteHandler(event:Event):void {
                
                //Get the returned JSON data string
                var response:String = event.target.data as String;
                
                //The list of returned videos is embedded in the "items" property
                //of the root JSON object, so we will decode to a container
                var container:Object = (JSON.decode(response) as Object);
                
                videosDataGrid.dataProvider = new ArrayCollection(container.items);
                
                //Convert the UNIX date into an AS3 Date
                for(var i:int = 0; i< videosDataGrid.dataProvider.length; i++) {
                    var video:Object = videosDataGrid.dataProvider.getItemAt(i);
                    var n:Number = video.publishedDate;
                    video.publishedDate = new Date(n);
                }           
                
            }
            
        ]]>
    </mx:Script>
    
    <!--
    ********************************************************************************************
    VIEW
    ********************************************************************************************
    -->
    <mx:Button id="loadButton"
               label="Load Vidoes"
               click="onButtonClick()"/>
    
    <mx:DataGrid id="videosDataGrid"
                 width="100%" height="100%">
        
        <mx:columns>
            <mx:DataGridColumn 
                dataField="id" 
                headerText="ID"/>
            <mx:DataGridColumn 
                dataField="name" 
                headerText="Name"/>
            <mx:DataGridColumn 
                dataField="shortDescription" 
                headerText="Description"/>
            <mx:DataGridColumn 
                dataField="publishedDate" 
                headerText="Published Date"/>
        </mx:columns>
    </mx:DataGrid>
    
</mx:Application>